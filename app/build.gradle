apply plugin: 'com.android.application'

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    defaultConfig {
        applicationId "com.termux.tasker"
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        versionCode 5
        versionName "0.5"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_TASKER_APP_NAME = "Termux:Tasker"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Reproducible builds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                def releaseTag = System.getenv("RELEASE_TAG")
                outputFileName = new File("termux-tasker" + (releaseTag ? "-" + releaseTag : "") + "-debug.apk")
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    implementation "androidx.annotation:annotation:1.2.0"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.guava:guava:24.1-jre'

    implementation 'com.termux.termux-app:termux-shared:819571a03a'

    // Use if below libraries are published locally by termux-app with `./gradlew publishReleasePublicationToMavenLocal` and used with `mavenLocal()`.
    // If updates are done, republish there and sync project with gradle files here
    // https://github.com/termux/termux-app/wiki/Termux-Libraries
    //implementation 'com.termux:termux-shared:0.117'
}

task versionName {
  doLast {
    print android.defaultConfig.versionName
  }
}
